apply plugin: 'maven-publish'

def properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

def LIB_GROUP_ID = 'custodia.ai'
def LIB_ARTIFACT_ID = System.getenv('LIB_ARTIFACT_ID') ?: properties.getProperty('LIB_ARTIFACT_ID') ?: 'sectionedrecyclerviewadapter'
def LIB_VERSION = '1.0.0'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/custodia-team/SectionedRecyclerViewAdapter")
            credentials {
                username = System.getenv('GITHUB_USER') ?: properties.getProperty('GITHUB_USER')
                password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: properties.getProperty('GITHUB_PERSONAL_ACCESS_TOKEN')
            }
        }
        maven {
            name = 'CustomMavenRepo'
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        custodia_sdk(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/library-release.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}